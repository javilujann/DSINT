//created on: Oct 23, 2025
package rule

//list any import classes here.
import elem.Ciclo
import elem.ECG
import componentes.*


//declare any global variables here


rule "Creacion de Intervalo PR"
	agenda-group "Reglas_Componentes"

    when
      $c : Ciclo( intervaloPR == null )
      $p: OndaP(ciclo == $c)
      $q: OndaQ( ciclo == $c)
    then   
        float _inicio = $p.getInicio();
        float _final = $q.getInicio();
        IntervaloPR intervalo = new IntervaloPR(_inicio, _final, $c);
    	
        modify($c){
        	setIntervaloPR(intervalo);
        }
        insert(intervalo);
end


rule "Creacion de ComplejoQRS"
	agenda-group "Reglas_Componentes"

    when
     	$c : Ciclo( complejoQRS == null )
      	$q: OndaQ(ciclo == $c)
     	$s: OndaS(ciclo == $c)
    then 
        float _inicio = $q.getInicio();
        float _final = $s.getFin();
        ComplejoQRS complejo = new ComplejoQRS(_inicio, _final, $c);
    	
        modify($c){
        	setComplejoQRS(complejo);
        }
        insert(complejo);
end


rule "Creacion de Intervalo QT"
	agenda-group "Reglas_Componentes"

    when
		$c : Ciclo( intervaloQT == null )
      	$q: OndaQ(ciclo == $c)
      	$t: OndaT(ciclo == $c)
    then
        float _inicio = $q.getInicio();
        float _final = $t.getFin();
        IntervaloQT intervalo = new IntervaloQT(_inicio, _final, $c);
    	
        modify($c){
        	setIntervaloQT(intervalo);
        }
        insert(intervalo);
end

rule "Calcular Intervalo RR"
	agenda-group "Reglas_Componentes"
    when
        $c1 : Ciclo( $indice : indice )
        $r1: OndaR(ciclo == $c1)
       	$c2 : Ciclo( indice == ($indice + 1), intervaloRR == null)
		$r2: OndaR(ciclo == $c2)
    then
        // Calcular punto medio de cada onda R
        float inicio = ($r1.getInicio() + $r1.getFin()) / 2;
        float fin = ($r2.getInicio() + $r2.getFin()) / 2;
        
        IntervaloRR intervalo = new IntervaloRR(inicio,fin, $c2);
        modify($c2){
        	setIntervaloRR(intervalo);
       }
       insert(intervalo);
end


rule "Calcular Frecuencia Cardiaca"
	agenda-group "Reglas_Componentes"
	
	when 
		$ECG : ECG(frecuenciaCardiaca == 0, $ciclos : ciclos)
		$o : Onda($fin : fin)
		not Onda(fin > $fin)
		
	then 
		int numeroCiclos = $ciclos.size();
		double frecuencia = (numeroCiclos * 60 * 1000) / (double)$fin;
		modify($ECG){
			setFrecuenciaCardiaca((int)frecuencia)
		}
end




