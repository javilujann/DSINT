//created on: Oct 23, 2025
package rule

//list any import classes here.
import elem.Ciclo
import elem.ECG
import componentes.*


//declare any global variables here


rule "Creacion de Intervalo PR"
	agenda-group "Reglas_Componentes"

    when
      $c : Ciclo( intervaloPR == null )
      $p: Onda(tipo == OndaTipo.P, ciclo == $c)
      $q: Onda(tipo == OndaTipo.Q, ciclo == $c)
    then   
        float _inicio = $p.getInicio();
        float _final = $q.getInicio();
        Intervalo intervalo = new Intervalo(_inicio, _final,IntervaloTipo.PR);
    	
        modify($c){
        	setIntervaloPR(intervalo);
        }
end

rule "Creacion de Segmento PR"
	agenda-group "Reglas_Componentes"

    when
      $c : Ciclo( segmentoPR == null )
      $p: Onda(tipo == OndaTipo.P, ciclo == $c)
      $q: Onda(tipo == OndaTipo.Q, ciclo == $c)
    then
        
        float _inicio = $p.getFin();
        float _final = $q.getInicio();
        Segmento segmento = new Segmento(_inicio, _final, SegmentoTipo.PR);
    	
        modify($c){
        	setSegmentoPR(segmento);
        }
end

rule "Creacion de ComplejoQRS"
	agenda-group "Reglas_Componentes"

    when
     	$c : Ciclo( complejoQRS == null )
      	$q: Onda(tipo == OndaTipo.Q, ciclo == $c)
     	$s: Onda(tipo == OndaTipo.S, ciclo == $c)
    then 
        float _inicio = $q.getInicio();
        float _final = $s.getFin();
        ComplejoQRS complejo = new ComplejoQRS(_inicio, _final);
    	
        modify($c){
        	setComplejoQRS(complejo);
        }
end

rule "Creacion de Segmento ST"
	agenda-group "Reglas_Componentes"

    when
		$c : Ciclo( segmentoST == null )
      	$s: Onda(tipo == OndaTipo.S, ciclo == $c)
      	$t: Onda(tipo == OndaTipo.T, ciclo == $c)
    then
        float _inicio = $s.getFin();
        float _final = $t.getInicio();
        Segmento segmento = new Segmento(_inicio, _final,SegmentoTipo.ST);
    	
        modify($c){
        	setSegmentoST(segmento);
        }
end

rule "Creacion de Intervalo QT"
	agenda-group "Reglas_Componentes"

    when
		$c : Ciclo( intervaloQT == null )
      	$q: Onda(tipo == OndaTipo.Q, ciclo == $c)
      	$t: Onda(tipo == OndaTipo.T, ciclo == $c)
    then
        float _inicio = $q.getInicio();
        float _final = $t.getFin();
        Intervalo intervalo = new Intervalo(_inicio, _final,IntervaloTipo.QT);
    	
        modify($c){
        	setIntervaloQT(intervalo);
        }
end

rule "Calcular Intervalo RR"
	agenda-group "Reglas_Componentes"
    when
        $c1 : Ciclo( $indice : indice )
        $r1: Onda(tipo == OndaTipo.R, ciclo == $c1)
       	$c2 : Ciclo( indice == ($indice + 1), intervaloRR == null)
		$r2: Onda(tipo == OndaTipo.R, ciclo == $c2)
    then
        // Calcular punto medio de cada onda R
        float inicio = ($r1.getInicio() + $r1.getFin()) / 2;
        float fin = ($r2.getInicio() + $r2.getFin()) / 2;
        
        Intervalo intervalo = new Intervalo(inicio,fin,IntervaloTipo.RR);
        modify($c2){
        	setIntervaloRR(intervalo);
       }
end


rule "Calcular Frecuencia Cardiaca"
	agenda-group "Reglas_Componentes"
	
	when 
		$ECG : ECG(frecuenciaCardiaca == 0, $ciclos : ciclos)
		$o : Onda($fin : fin)
		not Onda(fin > $fin)
		
	then 
		int numeroCiclos = $ciclos.size();
		double frecuencia = (numeroCiclos * 60 * 1000) / (double)$fin;
		modify($ECG){
			setFrecuenciaCardiaca((int)frecuencia)
		}
end




