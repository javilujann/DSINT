//created on: Nov 12, 2025
package rule

//imports
import elem.Ciclo
import elem.ECG
import componentes.*

//globlas
global java.util.concurrent.atomic.AtomicInteger cycleIdCounter;


rule "1. Crear Nuevo Ciclo en Onda P"
	agenda-group "Reglas_Ciclos"
    salience 10 
when
	$ECG: ECG()
    $p: OndaP(ciclo == null, $p_inicio: inicio)
    not OndaP(ciclo == null, inicio < $p_inicio)
    
 then
    int newId = cycleIdCounter.getAndIncrement();
    Ciclo $c = new Ciclo(newId);
    
    insert($c);
    
    modify($p) {
        setCiclo($c)
    };
    modify($c) {
        setOndaP($p)
    };
    modify($ECG){
   		addCiclo($c)
   };
   
end

rule "2. Asignar Ondas Q al Ciclo"
	agenda-group "Reglas_Ciclos"
    salience 5
when
    $c: Ciclo()
	$p: OndaP(ciclo == $c, $p_inicio: inicio)
    $onda: OndaQ(ciclo == null, inicio > $p_inicio, $onda_inicio: inicio)
    not OndaP(inicio > $p_inicio, inicio < $onda_inicio)
    
then
    modify($onda) {
        setCiclo($c)
    };
    
    modify($c) {
        setOndaQ($onda)
    };
 
end

rule "3. Asignar Ondas R al Ciclo"
	agenda-group "Reglas_Ciclos"
    salience 5
when
    $c: Ciclo()
	$p: OndaP(ciclo == $c, $p_inicio: inicio)
    $onda: OndaR(ciclo == null, inicio > $p_inicio, $onda_inicio: inicio)
    not OndaP(inicio > $p_inicio, inicio < $onda_inicio)
    
then
    modify($onda) {
        setCiclo($c)
    };
    
    modify($c) {
        setOndaR($onda)
    };

end


rule "4. Asignar Ondas S al Ciclo"
	agenda-group "Reglas_Ciclos"
    salience 5
when
    $c: Ciclo()
	$p: OndaP(ciclo == $c, $p_inicio: inicio)
    $onda: OndaS(ciclo == null, inicio > $p_inicio, $onda_inicio: inicio)
    not OndaP(inicio > $p_inicio, inicio < $onda_inicio)
    
then
    modify($onda) {
        setCiclo($c)
    };
    
    modify($c) {
        setOndaS($onda)
    };

end

rule "5. Asignar Ondas T al Ciclo"
	agenda-group "Reglas_Ciclos"
    salience 5
when
    $c: Ciclo()
	$p: OndaP(ciclo == $c, $p_inicio: inicio)
    $onda: OndaT(ciclo == null, inicio > $p_inicio, $onda_inicio: inicio)
    not OndaP(inicio > $p_inicio, inicio < $onda_inicio)
    
then
    modify($onda) {
        setCiclo($c)
    };
    
    modify($c) {
        setOndaT($onda)
    };

end